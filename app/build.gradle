apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def apiKeyPropertiesFile = rootProject.file("apikey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "chunlin.lunar.geolocation"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Storing Secret Keys in Android
        //
        // Reference:
        // 1. https://github.com/codepath/android_guides/wiki/Storing-Secret-Keys-in-Android#hidden-in-buildconfigs
        buildConfigField("String", "AZURE_EVENT_HUB_CONNECTION_STRING", apiKeyProperties['AZURE_EVENT_HUB_CONNECTION_STRING'])
        buildConfigField("String", "AZURE_EVENT_HUB_NAME", apiKeyProperties['AZURE_EVENT_HUB_NAME'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Solve the problem of java.lang.NoSuchMethodError: No static method metafactory
    //
    // Reference:
    // 1. https://stackoverflow.com/a/59448917/1177328
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    // This is to remove the error saying there are more than one file was found with OS independent path 'META-INF/LICENSE'
    //
    // References:
    // 1. https://stackoverflow.com/a/48231124/1177328
    // 2. https://groups.google.com/forum/#!topic/Android-ndk/vfMfld5FY7A
    packagingOptions {
        pickFirst 'META-INF/LICENSE.md'
        pickFirst 'META-INF/NOTICE.md'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.azure:azure-messaging-eventhubs:5.0.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
